package bear_Attack

import bear_Main
import Orders
import TimerUtils
import SoundUtils
import bear_Bleed
import RegisterEvents
import bear_Frenzy
import InstantDummyCaster

init
    registerSpellEffectEvent('A02A', function Attack.onHit)

public class Attack

    private static constant SoundDefinition array hitSounds = [
        new SoundDefinition(Sounds.woodHeavyBashFlesh1, false, true),
        new SoundDefinition(Sounds.woodHeavyBashFlesh2, false, true),
        new SoundDefinition(Sounds.woodHeavyBashFlesh3, false, true) ]
    
    private static constant real ATTACKRANGE = 200
    private static constant real UPDATEMOVE_FREQ = 0.5

    private static boolean isAttacking = true

    private static boolean onCooldown = false
    private static boolean updateMove = false 

    private static trigger trg_OnHit
    private static timer timer_UpdateMove
    private static timer timer_AttackCD


    static function activate()
        timer_AttackCD = getTimer()
        timer_UpdateMove = getTimer()
        updateMove = true

    static function deactivate()
        timer_AttackCD.release()
        timer_UpdateMove.release()


     static function start()
     
        if TheBear.theBear.getCurrentOrder() != Orders.fingerofdeath
            if TheBear.theBear.getPos().distanceTo(hunter.getPos()) <= ATTACKRANGE
                if not onCooldown
                    print("Ordering finger of death")
                    TheBear.theBear.issueTargetOrderById( Orders.fingerofdeath, hunter )
                else if updateMove
                    TheBear.theBear.setFacing(TheBear.theBear.getPos().angleTo(hunter.getPos()))
                    updateMove = false
                    timer_UpdateMove.start( UPDATEMOVE_FREQ, function setUpdateMove )
            else
                if updateMove
                    TheBear.theBear.issuePointOrderById( Orders.move, hunter.getPos() )
                    updateMove = false
                    timer_UpdateMove.start( UPDATEMOVE_FREQ, function setUpdateMove )
    

    private static function setUpdateMove()
        updateMove = true

    static function onHit()
        hitSounds[GetRandomInt(0, 2)].playOnPoint(hunter.getPos3Real())
        onCooldown = true
        timer_AttackCD.start(TheBear.currentAttackSpeed, function cooldownFinished )
        UnitDamageTarget(TheBear.theBear, hunter, TheBear.ATTACKDMG*(1+TheBear.damageInc), false, false, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_NORMAL, WEAPON_TYPE_WHOKNOWS) //NEED DAMAGE_TYPE_NORMAL IN ORDER TO TAKE ARMOR INTO ACCOUTN
        Bleed.addStack()

        if Frenzy.isActive
            InstantDummyCaster.castTarget(Player(11), 'A02D', 1, Orders.slow, hunter)
            

    private static function cooldownFinished()
        onCooldown = false
        

            
    
            





    